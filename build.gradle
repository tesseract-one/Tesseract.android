// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def ndkVersionProp = properties.getProperty('ndk.version')
    def targetsString = properties.getProperty('ndk.targets')
    def targetsProp = targetsString == null ? null : new ArrayList<>(Arrays.asList(targetsString.split(",")))

    def DEFAULT_NDK_VERSION = '25.1.8937393'
    def DEFAULT_NDK_TARGETS = ["arm", "x86", "arm64", "x86_64"]

    ext {
        compileSdkVersion = 33
        minSdkVersion = 24
        targetSdkVersion = 33
        ndkVersion = ndkVersionProp == null ? DEFAULT_NDK_VERSION : ndkVersionProp
        rustNdkTargets = targetsProp == null ? DEFAULT_NDK_TARGETS : targetsProp

        javaVersion = JavaVersion.VERSION_1_8
        javaTarget = '1.8'

        navVersion = '2.6.0'
    }
}

plugins {
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.3" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    group 'one.tesseract.android'
}
