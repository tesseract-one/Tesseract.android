plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'one.tesseract.android'

    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.javaTarget
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

task fixNotCopyingRustJniLibs(type: Delete) {
    delete layout.buildDirectory.dir("intermediates/merged_jni_libs")
}

project.afterEvaluate {
    tasks.getByName("mergeDebugJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("mergeReleaseJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("cargoBuild").dependsOn("fixNotCopyingRustJniLibs")
}

cargo {
    module  = "../../rust/tesseract"       // Or whatever directory contains your Cargo.toml
    libname = "tesseract"          // Or whatever matches Cargo.toml's [package] name.
    targets = rootProject.ext.rustNdkTargets  // See bellow for a longer list of options
    targetDirectory = '../../target'
    rustupChannel = "nightly"
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.navVersion"

    implementation project(':java:interop-rust')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}