plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'one.tesseract.common'
    version rootProject.ext.tesseractVersion

    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.javaTarget
    }
    publishing {
        singleVariant("release") {
        }
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

task fixNotCopyingRustJniLibs(type: Delete) {
    delete layout.buildDirectory.dir("intermediates/merged_jni_libs")
}

project.afterEvaluate {
    tasks.getByName("mergeDebugJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("mergeReleaseJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("cargoBuild").dependsOn("fixNotCopyingRustJniLibs")
}

cargo {
    module  = "../../rust/library"       // Or whatever directory contains your Cargo.toml
    libname = "tesseract"          // Or whatever matches Cargo.toml's [package] name.
    targets = rootProject.ext.rustNdkTargets  // See bellow for a longer list of options
    targetDirectory = '../../target'
    rustupChannel = "nightly"
    profile = rootProject.ext.rustNdkProfile
}

dependencies {
    implementation project(':java:crabdroid')//don't delete

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}